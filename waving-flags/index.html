<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <link rel="stylesheet" href="./index.css" />
    <title>WEBGL2</title>
</head>

<body onload="main()">
    <canvas id="happy-life-happy-code" width="500" height="500"></canvas>
    <select class="selector">
        <option value="frag-china">中国</option>
        <option value="frag-usa">美国</option>
        <option value="frag-germany">德国</option>
        <option value="frag-france">法国</option>
        <option value="frag-japan">日本</option>
        <option value="frag-english">英国</option>
        <option value="frag-korea">南韩</option>
    </select>
</body>
<script src="https://webgl2fundamentals.org/webgl/resources/twgl-full.min.js" type="text/javascript"></script>
<script src="https://webgl2fundamentals.org/webgl/resources/webgl-utils.js" type="text/javascript"></script>
<script src="../core/gl-matrix.js" type="text/javascript"></script>
<script src="https://webgl2fundamentals.org/webgl/resources/m4.js" type="text/javascript"></script>
<script src="https://webgl2fundamentals.org/webgl/resources/flattened-primitives.js"></script>

<script type="x-shader/x-vertex" id="vertex">#version 300 es
    in vec4 a_position;
    void main() {
        gl_Position = a_position;
    }
</script>
<script type="x-shader/x-fragment" id="frag-usa">#version 300 es
    precision mediump float;
    uniform vec2 u_resolution;
    uniform float u_time;
    out vec4 FragColor;

    /* Color palette */
    #define BLACK           vec3(0.0, 0.0, 0.0)
    #define WHITE           vec3(1.0, 1.0, 1.0)
    #define RED             vec3(178., 34., 52) / 255.
    #define BLUE            vec3(60., 59.0, 110.0) /255.

    #define PI            3.1415926535


    vec2 Remap(vec2 p, float t, float r, float b, float l) {
        return vec2((p.x - l) / (r - l), (p.y - b) / (t - b));
    }

    float sdStar5(in vec2 p, in float r, in float rf, vec2 offset)
    {
        p -= offset; // This will subtract offset.x from p.x and subtract offset.y from p.y
        const vec2 k1 = vec2(0.809016994375, -0.587785252292);
        const vec2 k2 = vec2(-k1.x,k1.y);
        p.x = abs(p.x);
        p -= 2.0*max(dot(k1,p),0.0)*k1;
        p -= 2.0*max(dot(k2,p),0.0)*k2;
        p.x = abs(p.x);
        p.y -= r;
        vec2 ba = rf*vec2(-k1.y,k1.x) - vec2(0,1);
        float h = clamp( dot(p,ba)/dot(ba,ba), 0.0, r );
        return length(p-ba*h) * sign(p.y*ba.x-p.x*ba.y);
    }

    float Start2(vec2 uv, vec2 num) {
        float ratio = 1.;
        float offset = 1.;
        float star;
        vec2 st = fract(uv * num);
        star = 1. - smoothstep(0., 0.005, sdStar5(st, .25 * ratio, .5, vec2(.5 * offset, 0.5)));
        return star;
    }


    void main() {
        vec2 uv = gl_FragCoord.xy / u_resolution;
        // uv *= 1.1;
        float t = uv.x * 7. - 2. * u_time + uv.y * 3.;
        uv.y +=  sin( t ) * .05;
        // caculate the min pix in y axios;
        float d = 0.01;
        vec3 c = mix(WHITE, RED, smoothstep(d, d + 0.2, sin(uv.y * PI * 13.)));

        vec3 color = c;

    

        color *= smoothstep(d, .005, abs(uv.y - .5) - .5 + d);

        vec2 st = Remap(uv, 1., .4, .46, 0.);

        if(st.x > .0 && st.x <= 1. && st.y <= 1. && st.y > 0.) {
            color = mix(
                mix(color, BLUE, smoothstep(0., .01, st.y)),
                WHITE,
                Start2(st, vec2(6., 5.))
            );

            st = Remap(st, .9, .9166, .1, .0833);

            if(st.x > .0 && st.x <= 1. && st.y <= 1. && st.y > 0.) {
                color = mix(color, WHITE, Start2(st, vec2(5., 4.)));
            }
        }

        color *= .7 + cos(t) * .3;
        FragColor = vec4(color, 1.);
    }
</script>

<script type="x-shader/x-fragment" id="frag-japan">#version 300 es
    precision mediump float;
    uniform vec2 u_resolution;
    uniform float u_time;
    out vec4 FragColor;

    /* Color palette */
    #define BLACK           vec3(0.0, 0.0, 0.0)
    #define WHITE           vec3(1.0, 1.0, 1.0)
    #define RED             vec3(1.0, 0.0, 0.0)

    #define PI            3.1415926535

    vec2 Remap(vec2 p, float t, float r, float b, float l) {
        return vec2((p.x - l) / (r - l), (p.y - b) / (t - b));
    }

    void main() {
        vec2 uv = (gl_FragCoord.xy - .5 * u_resolution) / u_resolution.y;
        
        vec3 bg = vec3(1.0);
        float w = fwidth(uv.y);
        float t = uv.x * 7. - 2. * u_time + uv.y * 5.;
        uv.y +=  sin( t ) * .05;
        bg = mix(bg, RED, (1.0 - smoothstep(.25, .253, length(uv))));
        uv.y += .5;
        uv.x -= .5;
        bg *= smoothstep(w, 0.0, abs(uv.y - .5) - .5 + w);
        bg *= .7 + cos(t) * .3;
        FragColor = vec4(bg, 1.0);
    }
</script>


<script type="x-shader/x-fragment" id="frag-germany">#version 300 es
    precision mediump float;
    uniform vec2 u_resolution;
    uniform float u_time;
    out vec4 FragColor;

    /* Color palette */
    #define BLACK           vec3(0.0, 0.0, 0.0)
    #define WHITE           vec3(1.0, 1.0, 1.0)
    #define RED             vec3(1.0, 0.0, 0.0)
    #define YELLOW          vec3(1.0, 1.0, 0.0)
    #define PI            3.1415926535

    vec2 Remap(vec2 p, float t, float r, float b, float l) {
        return vec2((p.x - l) / (r - l), (p.y - b) / (t - b));
    }

    void main() {
        const float ratio = 1.0 / 3.0;
        vec2 uv = gl_FragCoord.xy / u_resolution.xy;
        float w = fwidth(uv.y);
        float t = uv.x * 7. - 2. * u_time + uv.y * 3.;
        uv.y +=  sin( t ) * .05;

        vec3 a = YELLOW;
        a = mix(a, RED, step(ratio, uv.y));

        a = mix(a, BLACK, step(ratio * 2.0, uv.y));

        a *= smoothstep(w, 0.0, abs(uv.y - .5) - .5 + w);
        a *= .7 + cos(t) * .3;
        
        FragColor = vec4(a, 1.0);
    }
</script>


<script type="x-shader/x-fragment" id="frag-china">#version 300 es
    #define RED             vec3(1.0, 0.0, 0.0)
    #define YELLOW          vec3(1.0, 1.0, 0.0)

    precision mediump float;
    uniform vec2 u_resolution;
    uniform float u_time;
    out vec4 FragColor;

    mat2 rotate2D(float ang) {
        return mat2(cos(ang), -sin(ang), sin(ang), cos(ang));
    }

    float sdStar5(in vec2 p, in float r, in float rf, vec2 offset)
    {
        p -= offset; // This will subtract offset.x from p.x and subtract offset.y from p.y
        const vec2 k1 = vec2(0.809016994375, -0.587785252292);
        const vec2 k2 = vec2(-k1.x,k1.y);
        p.x = abs(p.x);
        p -= 2.0*max(dot(k1,p),0.0)*k1;
        p -= 2.0*max(dot(k2,p),0.0)*k2;
        p.x = abs(p.x);
        p.y -= r;
        vec2 ba = rf*vec2(-k1.y,k1.x) - vec2(0,1);
        float h = clamp( dot(p,ba)/dot(ba,ba), 0.0, r );
        return length(p-ba*h) * sign(p.y*ba.x-p.x*ba.y);
    }

    vec2 Remap(vec2 p, float t, float r, float b, float l) {
        return vec2((p.x - l) / (r - l), (p.y - b) / (t - b));
    }

    void main() {
        vec2 uv = gl_FragCoord.xy / u_resolution.xy;
        vec3 bg = RED;
        float w = fwidth(uv.y);
        float t = uv.x * 7. - 2. * u_time + uv.y * 3.;
        uv.y +=  sin( t ) * .05;
        vec2 st = Remap(uv, 1.0, .45, .3, 0.0);
        if(st.x >= 0.0 && st.x <=1.0 && st.y >=0.0 && st.y <= 1.0) {
            //bg = vec3(0.0);
            float star = (1. - smoothstep(0., 0.005, sdStar5(st, .15, .45, vec2(0.3, 1.0 - 0.33))));
            bg = mix(bg, YELLOW, star);

            float star1 = (1. - smoothstep(0., 0.005, sdStar5(st, .05, .45, vec2(0.5, 1.0 - 0.6))));
            bg = mix(bg, YELLOW, star1);

            float star2 = (1. - smoothstep(0., 0.005, sdStar5(st, .05, .45, vec2(0.7, 1.0 - 0.5))));
            bg = mix(bg, YELLOW, star2);

            float star3 = (1. - smoothstep(0., 0.005, sdStar5(st, .05, .45, vec2(0.5, 1.0 - 0.1))));
            bg = mix(bg, YELLOW, star3);
            st -= vec2(0.7, 0.75);
            st = rotate2D(20.0) * st;
            st += vec2(0.7, 0.75);
            float star4 = (1. - smoothstep(0., 0.005, sdStar5(st, .05, .45, vec2(0.7, 1.0 - 0.25))));
            bg = mix(bg, YELLOW, star4);

        }

        bg *= smoothstep(w, 0.0, abs(uv.y - .5) - .5 + w);
        bg *= .7 + cos(t) * .3;
        
    

        FragColor = vec4(bg, 1.0);
    }
</script>

<script type="x-shader/x-fragment" id="frag-france">#version 300 es
    #define WHITE           vec3(1.0, 1.0, 1.0)
    #define RED             vec3(178., 34., 52) / 255.
    #define BLUE            vec3(60., 59.0, 110.0) /255.

    precision mediump float;
    uniform vec2 u_resolution;
    uniform float u_time;
    out vec4 FragColor;

    void main() {
        const float ratio = 1.0 / 3.0;
        vec2 uv = gl_FragCoord.xy / u_resolution.xy;
        float w = fwidth(uv.y);
        float t = uv.x * 7. - 2. * u_time + uv.y * 3.;
        uv.y +=  sin( t ) * .05;

        vec3 a = BLUE;
        a = mix(a, WHITE, step(ratio, uv.x));

        a = mix(a, RED, step(ratio * 2.0, uv.x));

        a *= smoothstep(w, 0.0, abs(uv.y - .5) - .5 + w);
        a *= .7 + cos(t) * .3;
        
        FragColor = vec4(a, 1.0);
    }
</script>

<script type="x-shader/x-fragment" id="frag-english">#version 300 es
    #define WHITE           vec3(1.0, 1.0, 1.0)
    #define RED             vec3(178., 34., 52) / 255.
    #define BLUE            vec3(60., 59.0, 110.0) /255.

    precision mediump float;
    uniform vec2 u_resolution;
    uniform float u_time;
    out vec4 FragColor;

    float plot(vec2 st, float width) {
        return smoothstep(width + 0.005, width, abs(st.y - (1.0- st.x)));
    }
    
    float plot1(vec2 st, float width) {
        return smoothstep(width + 0.005, width, abs(st.y - st.x));
    }
    
    float strip_x(vec2 st, float start, float end) {
        return smoothstep(start, start + 0.005, st.x) - 
        smoothstep(end, end + 0.005, st.x);
    }
    
    float strip_y(vec2 st, float start, float end) {
        return smoothstep(start, start + 0.005, st.y) - 
        smoothstep(end, end + 0.005, st.y);
    }

    void main() {
        const float ratio = 1.0 / 3.0;
        vec2 uv = gl_FragCoord.xy / u_resolution.xy;
        float w = fwidth(uv.y);
        float t = uv.x * 7. - 2. * u_time + uv.y * 3.;
        uv.y +=  sin( t ) * .05;

        vec3 col = BLUE;
       
        float x1 = plot(uv, 0.1);
        
        col = mix(col, WHITE, x1);
        
        float x2 = plot1(uv, 0.1);
        
        col = mix(col, WHITE, x2);
        
        float x3 = plot(uv, .02);
        
        col = mix(col, RED, x3);
        
        float x4 = plot1(uv, .02);
        
        col = mix(col, RED, x4);
        
    
        float x5 = strip_x(uv, .45, .55);
        
        col = mix(col, WHITE, x5);
        
        float x6 = strip_y(uv, .4, .6);
        
        col = mix(col, WHITE, x6);
        
        float x7 = strip_x(uv, .47, .53);
        
        col = mix(col, RED, x7);
        
        float x8 = strip_y(uv, .45, .55);
        
        col = mix(col, RED, x8);


        col *= smoothstep(w, 0.0, abs(uv.y - .5) - .5 + w);
        col *= .7 + cos(t) * .3;
        
        FragColor = vec4(col, 1.0);
    }
</script>

<script type="x-shader/x-fragment" id="frag-korea">#version 300 es

#define PI    3.1415926535
#define WHITE           vec3(1.0, 1.0, 1.0)
#define RED             vec3(178., 34., 52) / 255.
#define BLUE            vec3(60., 59.0, 110.0) /255.
#define BLACK           vec3(0.0)

precision mediump float;
uniform vec2 u_resolution;
uniform float u_time;
out vec4 FragColor;

vec2 Remap(vec2 p, float t, float r, float b, float l) {
    return vec2((p.x - l) / (r - l), (p.y - b) / (t - b));
}
//sin(x * PI * 2) * .13 + .4
float plot(vec2 uv) {
    return smoothstep(
        sin(uv.x * PI*2.0) * .1 + .5,
        sin(uv.x * PI*2.0) *.1 + 0.51,
        uv.y
    );
}

float strip_x(vec2 uv, float start, float end) {
    return smoothstep(start, start + 0.01, uv.x) -
        smoothstep(end, end + 0.01, uv.x);
}

float strip_y(vec2 uv, float start, float end) {
     return smoothstep(start, start + 0.01, uv.y) -
        smoothstep(end, end + 0.01, uv.y);
}

float rect(vec2 uv, float start_x, float end_x,
float start_y, float end_y) {
    
   return strip_x(uv, start_x, end_x) *
       strip_y(uv, start_y, end_y);

}

mat2 rotate2D(float _a) {
    return mat2(cos(_a), -sin(_a), sin(_a), cos(_a));
}

mat2 scale2D(vec2 s) {
    return mat2(s.x, 0.0, 0.0, s.y);
}





void main() {
    // Normalized pixel coordinates (from 0 to 1)
    //vec2 uv = fragCoord/iResolution.xy;
    vec2 uv = (gl_FragCoord.xy - .5 * u_resolution.xy)/u_resolution.y;
    
    // Time varying pixel color
    vec3 col = vec3(1.0);
    vec2 st = Remap(uv, .35, -.35, -.35, .35);
    
    float circle = 1.0 - smoothstep(0.34, 0.35, length(uv));
    //col = mix(col, RED, circle);
    vec3 circleColor = circle * vec3(1.0);
    vec3 taiji = vec3(1.0);
    if(st.x >=0.0 && st.x <= 1.0 && st.y >=0.0 && st.y <= 1.0) {
        taiji = BLUE;
        float t = plot(st);
        taiji = mix(taiji, RED, t);
        // point
        col -= circleColor - taiji;;
    }
    
    // top left
    vec2 c0 = Remap(uv, .45, -.45, 0.1, -.8);
    
    c0 -= vec2(0.5);
    c0 = rotate2D(100.0) * c0;
    c0 *= scale2D(vec2(2.0, 1.5));
    c0 += vec2(0.5);
    
    
    if(c0.x >=0.0 && c0.x <= 1.0 && c0.y >=0.0 && c0.y <= 1.0) {
        
        col = BLACK;
        float x1 = strip_x(c0, .3, .35);
        col = mix(col, WHITE, x1);
        
        float x2 = strip_x(c0, .65, .7);
        col = mix(col, WHITE, x2);
        
        
    }
    
    
    
    // top right
    // ba gua
    vec2 c1 = Remap(uv, .45, .45, 0.1, .8);
    
    c1 -= vec2(0.5);
    c1 = rotate2D(100.0) * c1;
    c1 *= scale2D(vec2(2.0, 1.5));
    c1 += vec2(0.5);
    
    
    if(c1.x >=0.0 && c1.x <= 1.0 && c1.y >=0.0 && c1.y <= 1.0) {
        
        col = BLACK;
        float x1 = strip_x(c1, .3, .35);
        col = mix(col, WHITE, x1);
        
        float x2 = strip_x(c1, .65, .7);
        col = mix(col, WHITE, x2);
        
        float y = rect(c1, -0.01, .31, .47, .53);
        col = mix(col, WHITE, y);
        
        float y2 = rect(c1, 0.69, 1.01, .47, .53);
        col = mix(col, WHITE, y2);
        
        
    }
    
    // bottom left
    
    vec2 c2 = Remap(uv, -.45, .45, -0.1, .8);
    
    c2 -= vec2(0.5);
    c2 = rotate2D(100.0) * c2;
    c2 *= scale2D(vec2(2.0, 1.5));
    c2 += vec2(0.5);
    
    
    if(c2.x >=0.0 && c2.x <= 1.0 && c2.y >=0.0 && c2.y <= 1.0) {
        
        col = BLACK;
        float x1 = strip_x(c2, .3, .35);
        col = mix(col, WHITE, x1);
        
        float x2 = strip_x(c2, .65, .7);
        col = mix(col, WHITE, x2);
        
        float y = rect(c2, -0.01, 1.01, .47, .53);
        col = mix(col, WHITE, y);
        
        float y2 = rect(c2, 0.69, 1.01, .47, .53);
        col = mix(col, WHITE, y2);
        
        
    }
    
    vec2 c3 = Remap(uv, -.45, -.45, -0.1, -.8);
    
    c3 -= vec2(0.5);
    c3 = rotate2D(100.0) * c3;
    c3 *= scale2D(vec2(2.0, 1.5));
    c3 += vec2(0.5);
    
    
    if(c3.x >=0.0 && c3.x <= 1.0 && c3.y >=0.0 && c3.y <= 1.0) {
        
        col = BLACK;
        float x1 = strip_x(c3, .3, .35);
        col = mix(col, WHITE, x1);
        
        float x2 = strip_x(c3, .65, .7);
        col = mix(col, WHITE, x2);
        
        float y = rect(c3, 0.35, .65, .47, .53);
        col = mix(col, WHITE, y);
        
        
    }

    // Output to screen
    FragColor = vec4(col, 1.0);
}
</script>

<script type="text/javascript" src="./index.js"></script>

</html>